apply plugin: 'de.marcphilipp.nexus-publish'
apply plugin: 'signing'

private def validateTagAndVersionForPublishing() {
    if (gitTag().isEmpty()) {
        throw new IllegalStateException('Publishing is not allowed because current commit has no tag')
    }

    final String projectVersion = version

    if (projectVersion == null || projectVersion.isEmpty()) {
        throw new IllegalStateException('Publishing is not allowed because current projectVersion is null or empty')
    }

    def semVerComponents = projectVersion.split('\\.')

    if (semVerComponents.length != 3) {
        throw new IllegalStateException(
            "Publishing is not allowed because current projectVersion " + "is not semantic, should have 3 components. projectVersion = $projectVersion")
    }

    for (int i = 0; i < semVerComponents.length - 1; i++) {
        if (!semVerComponents[i].isInteger() || Integer.parseInt(semVerComponents[i]) < 0) {
            throw new IllegalStateException(
                    "Publishing is not allowed because current projectVersion " + "is not semantic. First two components should be integers. projectVersion = $projectVersion")
        }
    }
}

task validatePublishing {
    doLast {
        validateTagAndVersionForPublishing()
    }
}

afterEvaluate {
    signing {
        required { gradle.taskGraph.hasTask("publishToSonatype") }
        sign publishing.publications.LibraryPublication
    }
}

nexusPublishing {
    repositories {
        sonatype {
            username = System.getenv("MAVEN_CENTRAL_USERNAME")
            password = System.getenv("MAVEN_CENTRAL_PASSWORD")
        }
    }
}

publishToSonatype.dependsOn validatePublishing
