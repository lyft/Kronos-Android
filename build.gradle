// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.3.21'
    repositories {
        google()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:0.9.18"
        // Uploads artifacts to oss.sonatype.org
        classpath "de.marcphilipp.gradle:nexus-publish-plugin:0.3.0"
        // "Closes" and "Releases" repository on oss.sonatype.org to finish release process.
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.21.0"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

apply from: "dependencies.gradle"

group = 'com.lyft.kronos'
version = projectVersion()

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects {
    group = rootProject.group
    version = rootProject.version
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

private def gitTag() {
    def tag = 'git tag --list --points-at HEAD'.execute((List) null, rootProject.projectDir).text.trim()

    if (tag.split(System.lineSeparator()).length > 1) {
        throw new IllegalStateException("gitTag is accessed but commit has multiple tags: $tag")
    }

    return tag
}

private def projectVersion() {
    def tag = gitTag()

    if (tag.startsWith('v')) {
        return tag.substring(1)
    } else if (tag.isEmpty()) {
        return 'development'
    }

    return tag
}

apply plugin: 'io.codearte.nexus-staging'

nexusStaging {
    packageGroup = "com.lyft"
    username = System.getenv("MAVEN_CENTRAL_USERNAME")
    password = System.getenv("MAVEN_CENTRAL_PASSWORD")
}
